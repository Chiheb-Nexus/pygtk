AC_INIT

dnl for gnome-python:

AM_INIT_AUTOMAKE(pygtk, 0.7.0)

AM_DISABLE_STATIC
AC_PROG_LIBTOOL

AM_PATH_PYTHON(module)

dnl stuff for python extension modules ...
py_prefix=`$PYTHON -c "import sys; print sys.prefix"`
py_exec_prefix=`$PYTHON -c "import sys; print sys.exec_prefix"`
PYTHON_INCLUDES="-I${py_prefix}/include/python${PYTHON_VERSION}"
if test "$py_prefix" != "$py_exec_prefix"; then
  PYTHON_INCLUDES="$PYTHON_INCLUDES -I${py_exec_prefix}/include/python${PYTHON_VERSION}"
fi
AC_SUBST(PYTHON_INCLUDES)

AM_CHECK_PYMOD(thread,,extra_mods=gthread,extra_mods=)

AM_PATH_GTK(1.2.1,,,$extra_mods)
AM_PATH_GDK_IMLIB(1.8, build_imlib=true, build_imlib=false)
AM_CONDITIONAL(BUILD_IMLIB, $build_imlib)

AM_PATH_GTKGL(build_gtkgl=true, build_gtkgl=false)
AM_CONDITIONAL(BUILD_GTKGL, $build_gtkgl)

dnl what to do about libglade-gnome ?
AM_PATH_LIBGLADE(build_libglade=true, build_libglade=false)
AM_CONDITIONAL(BUILD_LIBGLADE, $build_libglade)

dnl checks to see if Numeric Python is installed.
save_CPPFLAGS="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $PYTHON_INCLUDES"
AC_CHECK_HEADER(arrayobject.h, AC_DEFINE(HAVE_NUMPY))
CPPFLAGS="$save_CPPFLAGS"

AC_OUTPUT(Makefile codegen/Makefile gtk/Makefile)


